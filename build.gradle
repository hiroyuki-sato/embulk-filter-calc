plugins {
    id "java"
    id "checkstyle"
    id "antlr"
    id "maven-publish"
    id "signing"
    id "org.embulk.embulk-plugins" version "0.5.5"
}
repositories {
    mavenCentral()
}

group = "io.github.hiroyuki-sato"
version = "0.2.0"
description = "Caculator filter plugin for Embulk"


dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"
    //compileOnly "org.embulk:embulk-api:0.11.0"

    implementation("org.embulk:embulk-util-config:0.3.4") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7.5"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    implementation "javax.validation:validation-api:1.1.0.Final"
    //implementation "joda-time:joda-time:2.9.2"

    antlr "org.antlr:antlr4:4.9.3"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "org.embulk:embulk-spi:0.11"
    //testImplementation "org.embulk:embulk-api:0.11.0"
    //testImplementation  "org.embulk:embulk-standards:0.10.42"

    // TODO: Remove them.
    // These `testImplementation` are a tentative workaround. It will be covered in Embulk core's testing mechanism.
    testImplementation "org.embulk:embulk-deps:0.11.0"
    testImplementation "com.google.guava:guava:18.0"
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}


checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '8.23'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

generateGrammarSource {
//    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages", "-no-listener"]
}

embulkPlugin {
    mainClass = "org.embulk.filter.calc.CalcFilterPlugin"
    category = "filter"
    type = "calc"
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://github.com/hiroyuki-sato/embulk-filter-calc"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Hiroyuki Sato"
                        email = "hiroysato@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/hiroyuki-sato/embulk-filter-calc.git"
                    developerConnection = "scm:git:git@github.com:hiroyuki-sato/embulk-filter-calc.git"
                    url = "https://github.com/hiroyuki-sato/embulk-filter-calc"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

gem {
    from("LICENSE.txt")  // Optional -- if you need other files in the gem.
    authors = [ "Hiroyuki Sato" ]
    email = [ "hiroysato@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Caculator filter plugin for Embulk"
    homepage = "https://github.com/hiroyuki-sato/embulk-filter-calc"
    licenses = [ "MIT" ]
    // metadata = [ ]
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'
}
