plugins {
    id "java"
    id "checkstyle"
    id "antlr"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "com.github.hiroyuki_sato"
version = "0.1.1-SNAPSHOT"
description = "Caculator filter plugin for Embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-spi:0.10.+"
    compileOnly "org.embulk:embulk-api:0.10.+"

    compile("org.embulk:embulk-util-config:0.2.+") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    testCompile "junit:junit:4.+"
    antlr "org.antlr:antlr4:4.9.1"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.10.+"
    testCompile "org.embulk:embulk-core:0.10.+:tests"
    testCompile "org.embulk:embulk-spi:0.10.+"
    testCompile "org.embulk:embulk-api:0.10.+"
    testCompile  "org.embulk:embulk-standards:0.10.+"

    // TODO: Remove them.
    // These `testCompile` are a tentative workaround. It will be covered in Embulk core's testing mechanism.
    testCompile "org.embulk:embulk-deps-buffer:0.10.+"
    testCompile "org.embulk:embulk-deps-config:0.10.+"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

generateGrammarSource {
//    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages", "-no-listener"]
}

embulkPlugin {
    mainClass = "org.embulk.filter.calc.CalcFilterPlugin"
    category = "filter"
    type = "calc"
}

// TODO: enable when the pure-java plugin publish.
//publishing {
//    publications {
//        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
//            from components.java  // Must be "components.java". The dependency modification works only for it.
//        }
//    }
//    repositories {
//        maven {
//            url = "${project.buildDir}/mavenPublishLocal"
//        }
//    }
//}

gem {
    from("LICENSE.txt")  // Optional -- if you need other files in the gem.
    authors = [ "Hiroyuki Sato" ]
    email = [ "hiroysato@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Caculator filter plugin for Embulk"
    homepage = "https://github.com/hiroyuki-sato/embulk-filter-calc"
    licenses = [ "MIT" ]
    // metadata = [ ]
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'
}
